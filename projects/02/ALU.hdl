// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
          // handle input zeroing (zx and zy)
      Mux16(a=x[0..15], b[0..15]=false, sel=zx, out=xmux);
      Mux16(a=y[0..15], b[0..15]=false, sel=zy, out=ymux);

      // handle input negation (nx and ny)
      Not16(in=xmux, out=notx);
      Mux16(a=xmux, b=notx, sel=nx, out=processedx);
      Not16(in=ymux, out=noty);
      Mux16(a=ymux, b=noty, sel=ny, out=processedy);

      // handle f input bit (function select)
      And16(a=processedx, b=processedy, out=outAnd);
      Add16(a=processedx, b=processedy, out=outAdd);
      Mux16(a=outAnd, b=outAdd, sel=f, out=outF);

      // handle no input bit (output negation) and ng output flag (output is < 0)
      Not16(in=outF, out=notoutF);
      Mux16(a=outF, b=notoutF, sel=no, out=out, out[15]=ng, out[0..7]=lsb, out[8..15]=msb);

      // handle zr output flag (if both zr are zero, zr==1)
      Or8Way(in=lsb, out=orlsbOut);
      Or8Way(in=msb, out=ormsbOut);
      Or(a=orlsbOut, b=ormsbOut, out=processedorOut);
      Not(in=processedorOut, out=zr);
}
